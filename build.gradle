plugins {
    id 'com.gradle.build-scan' version '1.6'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'nu.studer.plugindev' version '1.0.6'
    id 'nu.studer.credentials' version '1.0.3'
}

group = 'nu.studer'
version = '0.4'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

buildScan {
    licenseAgreementUrl 'https://gradle.com/terms-of-service'
    licenseAgree 'yes'
}

dependencies {
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

plugindev {
    pluginDescription 'Gradle plugin that integrates the Rocker template engine.'
    pluginImplementationClass 'nu.studer.gradle.rocker.RockerPlugin'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'rocker'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-rocker-plugin'
    projectInceptionYear '2018'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

test {
    maxParallelForks = 2
}

def testAll = tasks.create('testAll') {
    group = 'Gradle cross-version test'
}

List<String> testedGradleVersions = []
(3..4).each { testedGradleVersions << "3.$it" }
//testedGradleVersions << "3.2.1"
testedGradleVersions << "3.4.1"
testedGradleVersions << "3.5-rc-2"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}
