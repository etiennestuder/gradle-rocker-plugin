import org.gradle.api.plugins.jvm.JvmTestSuite
import org.gradle.util.GradleVersion

import static java.lang.Integer.parseInt

plugins {
    id 'java-gradle-plugin'
    alias(libs.plugins.publish)
    alias(libs.plugins.github.actions)
    id 'groovy'
}

group = 'nu.studer'
version = '3.2.1-DEV'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

def dvBuildScan = develocity.buildScan
testing {
    suites {
        test {
            dependencies {
                implementation libs.spock
                runtimeOnly libs.junit5.launcher
            }
            targets {
                all {
                    testTask.configure { Test t ->
                        t.maxParallelForks = 1 // there is currently only a single test class
                        t.useJUnitPlatform()

                        String testJavaRuntimeVersion = findProperty('testJavaRuntimeVersion') ?: '25'
                        String testGradleVersion = findProperty('testGradleVersion') ?: GradleVersion.current().version

                        t.javaLauncher.set(javaToolchains.launcherFor { spec ->
                            spec.languageVersion.set(JavaLanguageVersion.of(testJavaRuntimeVersion))
                            dvBuildScan.value(t.identityPath.asString() + "#jvmVersion", testJavaRuntimeVersion)
                        } as Provider<? extends JavaLauncher>)

                        t.systemProperty 'testContext.gradleVersion', testGradleVersion
                        dvBuildScan.value(t.identityPath.asString() + "#gradleVersion", testGradleVersion)

                        def incompatibleJavaVsGradleVersions =
                                parseInt(testJavaRuntimeVersion) < 17 && GradleVersion.version(testGradleVersion) >= GradleVersion.version('9.0') ||
                                parseInt(testJavaRuntimeVersion) > 20 && GradleVersion.version(testGradleVersion) < GradleVersion.version('8.6') ||
                                parseInt(testJavaRuntimeVersion) > 16 && GradleVersion.version(testGradleVersion) < GradleVersion.version('7.3') ||
                                parseInt(testJavaRuntimeVersion) > 15 && GradleVersion.version(testGradleVersion) < GradleVersion.version('7.0')
                        if (incompatibleJavaVsGradleVersions) {
                            t.enabled = false
                        }
                    }
                }
            }
        }
    }
}

tasks.withType(AbstractCompile).configureEach {
    options.compilerArgs <<
            "-Werror" <<
            "-Xlint:all"
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

gradlePlugin {
    website = 'https://github.com/etiennestuder/gradle-rocker-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-rocker-plugin'
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.rocker'
            displayName = 'gradle-rocker-plugin'
            description = 'Gradle plugin that integrates the Rocker template engine.'
            tags.set(['rocker'])
            implementationClass = 'nu.studer.gradle.rocker.RockerPlugin'
        }
    }
}

tasks.withType(ValidatePlugins.class).configureEach {
    failOnWarning = true
    enableStricterValidation = true
}
